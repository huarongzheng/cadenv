### Verbose option
if ( $?SOURCE_VERBOSE ) echo Sourcing /tools/utils/cadenv/rc/cmn.tcshrc

#  Group and Software login from .cshrc_config
if ( -e /usr/local/config/exec_cshrc ) source /usr/local/config/exec_cshrc 

if ($?prompt) then
  set ignoreeof
  set history=20
  set savehist=50
  if ($?saveprompt) then
      set prompt=$saveprompt
  endif
else
  # rsh does not execute .login but we expect this
  if ( ! $?UsrLocalBinLogin) then
     source /etc/csh.login
     source /usr/local/bin/dotlogin
     source ~/.login
  endif
endif

if ( ! $?PRO ) then
  set cshrc_project=""
  if ( $?PROJECT_NEWSHELL ) then
    set cshrc_project=$PROJECT_NEWSHELL
    unsetenv PROJECT_NEWSHELL
  else if ( $?PROJECT ) then
    set cshrc_project=$PROJECT
  endif
  if ( "$cshrc_project" != "" ) then
    if (-r /pro/$cshrc_project/.cshrc) source /pro/$cshrc_project/.cshrc
  endif
  unset cshrc_project
endif

### set variable "interactivesh" when attached to a tty

tty >& /dev/null
if ($status == 0) set interactivesh=1

### Get current architecture

if (! $?PLATFORM ) then
  if (-x /usr/local/bin/arch.guess) then
    setenv PLATFORM "`/usr/local/bin/arch.guess || echo unknown`"
  else
    setenv PLATFORM unknown
  endif
endif

### Set SHELL
setenv SHELL /usr/local/bin/tcsh

### Kerberos path

if ( "${path}" !~ */usr/kerberos/bin* ) then
  set path = ( /usr/kerberos/bin $path )
endif

### Add platform specific shell settings

if ( -r ~/$PLATFORM/.cshrc ) source ~/$PLATFORM/.cshrc

### EXE path

set path = ( $path /tools/utils/cadenv/bin )

### Set umask

umask 0002

### Add module loader

if ( -r /tools/Modules/init/tcsh ) source /tools/Modules/init/tcsh

### Interactive shell settings

if ( $?interactivesh ) then

  set filec      # influences filename completion in interactive shell
  set autolist   #  ''  automatic listing of alternatives during filec
  set ignoreeof  #  ''  whether Cntrl-D can be "exit" command
  set history=60

  set ellipsis=1
  set promptchars='$#'
  set prompt="%B${shell:t} %n@%m%b %C06 (%h)\n%# "

# alias precmd echo -n "]2\;"\$"{SHELL:t} "\$"USER@"\$"{HOST:r:r:r:r} "\$"cwd "
  alias precmd 'if ( -o $HOME/.message ) echo "\033[1m### MESSAGES FROM SYSTEM\033[m"; if ( -o $HOME/.message ) cat $HOME/.message; if ( -o $HOME/.message ) cat $HOME/.message >>$HOME/.message.log; if ( -o $HOME/.message ) rm $HOME/.message'
  alias type which

endif

if ( $?loginsh ) then

endif

